start: command
?command: pipe | seq | call
pipe: call "|" call | pipe "|" call
seq: command ";" command

call: _WS? (redirection _WS)* argument (_WS atom)* _WS?
?atom: redirection | argument
argument: (quoted | UNQUOTED)+
redirection: "<" _WS? argument -> l_dir
           | ">" _WS? argument -> r_dir

?quoted: single_quoted | double_quoted | back_quoted
single_quoted: "'" NON_NEWLINE_NON_SINGLE_QUOTE "'"
back_quoted: "`" NON_NEWLINE_NON_BACKQUOTE  "`"
double_quoted: "\"" (back_quoted | DOUBLE_QUOTE_CONTENT)* "\""

NON_KEYWORD: /[^\n'"`;|]+/

UNQUOTED: /[^ \t'"`\n;|<>]+/

NON_NEWLINE_NON_SINGLE_QUOTE: /[^\n']+/
NON_NEWLINE_NON_BACKQUOTE: /[^\n`]+/
DOUBLE_QUOTE_CONTENT: /[^\n"`]+/

%import common.WS_INLINE -> _WS